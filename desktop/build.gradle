apply plugin: 'java'

sourceCompatibility = 8.0
targetCompatibility = 8.0
compileJava.options.incremental = true

sourceSets.main.java.srcDirs = ['src/']
sourceSets.main.resources.srcDirs += ['resources/']
sourceSets.main.resources.srcDirs += [rootProject.file('assets').path]

project.ext.mainClassName = "com.metaphore.bmfmetaedit.desktop.DesktopLauncher"
project.ext.resourceDirs = files(
        'resources/',
        '../assets')

dependencies {
    runtime files(project.resourceDirs)
    implementation project(":core")
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath(sourceSets.main.runtimeClasspath)
    classpath(project.resourceDirs)
    workingDir = rootProject.file('assets').path
    standardInput = System.in
    setIgnoreExitValue(true)

    if (org.apache.tools.ant.taskdefs.condition.Os.isFamily(org.apache.tools.ant.taskdefs.condition.Os.FAMILY_MAC)) {
        jvmArgs '-XstartOnFirstThread'
    }
}

task dist(type: Jar) {
    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.main.output.resourcesDir)
    from { configurations.compile.collect { zipTree(it) } }
    from files(project.resourceDirs);

    manifest {
        attributes 'Main-Class': project.mainClassName
    }
}
dist.dependsOn classes